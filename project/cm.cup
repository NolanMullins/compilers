/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
//import java_cup.runtime.*;
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal IF, THEN, ELSE, END, RETURN, WHILE;
terminal ASSIGN, EQ, LT, GT, LTE, GTE, NE, PLUS, MINUS, TIMES, OVER, LPAREN, RPAREN, LSQBRK, RSQBRK, SEMI, COMMA;
terminal String ID, NUM, INT, VOID;
terminal ERROR;
   
/*non terminal Exp program1;*/
non terminal DecList dec_seq, program;
non terminal VarDecList local_decs, param_seq, params;
non terminal ExpList stmt_list, args;
non terminal Exp stmt, com_exp, simple_exp;
non terminal Exp exp, factor;
non terminal CompoundExp cmpd_stmt;
non terminal Dec dec;
non terminal Var var;
non terminal VarDec dec_var, param; 
non terminal NameTy type_spec;
non terminal Integer relop, op;

/* ----------------------------Grammar Section-------------------- */
   
start with program;

/* 1. {Program}  */
program	      ::= dec_seq:s
                 {: RESULT = s; :}
              ;

/* 2. Declaration List  */
dec_seq         ::= dec_seq:s1 dec:s2
                    {:  
                        DecList t = s1;
                        if (t.head == null) {
                            t.head = s2;
                        } else {
                            while( t.tail != null )
                                t = t.tail;
                            t.tail = new DecList(s2, null);
                        }
                        RESULT = s1;
                    :}
                |   dec:s 
                    {: RESULT = new DecList(s, null); :}
                |   error:e SEMI
                    {: 
                        parser.report_error("invalid decleration", null); 
                        RESULT = new DecList(null, null); 
                    :}
               ;

/* 6. Function Declaration */
/* 3. Declarations */
dec             ::= dec_var: v
                    {: RESULT = v; :}
                |   type_spec:t ID:i LPAREN params:p RPAREN cmpd_stmt:c
                    {: RESULT = new FunctionDec(tleft, tright, t, i, p, c); :}
                ;

/* 4. Variable Declaration, used by dec and local_decs */
dec_var         ::= type_spec:t ID:i SEMI
                    {: RESULT = new SimpleDec(tleft, tright, t, i); :}
                |   type_spec:t ID:i LSQBRK NUM:n RSQBRK SEMI
                    {: RESULT = new ArrayDec(tleft, tright, t, i, new IntExp(nleft, nright, n)); :}
                ;

/* 5. Type Specifier  */
type_spec       ::= INT:i
                    {: RESULT = new NameTy(ileft, iright, NameTy.INT); :}
                |   VOID:v
                    {: RESULT = new NameTy(vleft, vright, NameTy.VOID); :}
                ;

/* 7. Paramaters  */
params          ::= param_seq:s
                    {: RESULT = s; :}
                |   VOID:v
                    {: RESULT = new VarDecList(null, null); :}
                ;

/* 8. Paramaters List  */
param_seq       ::= param_seq:s COMMA param:p
                    {: 
                        VarDecList v = s;
                        if (v.head == null) {
                            v.head = p;
                        } else {
                            while( v.tail != null )
                                v = v.tail;
                            v.tail = new VarDecList(p, null);
                        }
                        RESULT = s;
                    :}
                |   param:p
                    {: RESULT = new VarDecList(p, null); :}
                ;

/* 9. Parameter  */
param           ::= type_spec:t ID:i 
                    {: RESULT = new SimpleDec(tleft, tright, t, i); :}
                |   type_spec:t ID:i LSQBRK RSQBRK
                    {: RESULT = new ArrayDec(tleft, tright, t, i, null); :}
                ;

/* 10. Compound Statement */
cmpd_stmt       ::= THEN:t local_decs:l stmt_list:s END:e
                    {: RESULT = new CompoundExp(tleft, tright, l, s); :}
                |   THEN:t error END:e
                    {: RESULT = new CompoundExp(tleft, tright, null, null); :}
                ;

/* 11. Local Declarations */
local_decs      ::= local_decs:l dec_var:d
                    {: 
                        VarDecList v = l;
                        if (v.head == null) {
                            v.head = d;
                        } else {
                            while( v.tail != null )
                                v = v.tail;
                            v.tail = new VarDecList(d, null);
                        }
                        RESULT = l;
                    :}
                |   
                    {: RESULT = new VarDecList(null, null); :}
                ;

/* 12. Statement List */
stmt_list       ::= stmt_list:l stmt:s
                    {: 
                        ExpList e = l;
                        if (e.head == null) {
                            e.head = s;
                        } else {
                            while( e.tail != null )
                                e = e.tail;
                            e.tail = new ExpList(s, null);
                        }
                        RESULT = l;
                    :}
                |   
                    {: RESULT = new ExpList(null, null); :}
                ;

/* 13. Statement */
/* 14. Expression Statement */
/* 15. Selection Statement */
/* 16. Iteration Statement */
/* 17. Return Statement */
/* Combined Rule 14, 15, 16, 17 into rule 13*/
stmt          ::= exp:e SEMI:s
                  {: RESULT = e; :}
              |   SEMI:s
                  {: RESULT = new EpsilonExp(sleft, sright); :}
              |   cmpd_stmt:c
                  {: RESULT = c; :}
              |   IF:i LPAREN exp:e RPAREN stmt:s
                  {: RESULT = new IfExp(ileft, iright, e, s, null); :}
              |   IF:i LPAREN exp:e RPAREN stmt:s1 ELSE stmt:s2
                  {: RESULT = new IfExp(ileft, iright, e, s1, s2); :}
              |   WHILE:w LPAREN exp:e RPAREN stmt:s
                  {: RESULT = new WhileExp(wleft, wright, e, s); :}
              |   RETURN:r exp:e SEMI
                  {: RESULT = new ReturnExp(rleft, rright, e); :}
              |   RETURN:r SEMI
                  {: RESULT = new ReturnExp(rleft, rright, null); :}
              |   error:e SEMI:s
                  {:
                    parser.report_error("invalid statement", null); 
                    RESULT = new EpsilonExp(sleft, sright); 
                  :}  
              ;
              

/* 18. Expression */
/* 20. Simple Expression */
/* Combined Rule 20 into rule 18*/
exp           ::= var:v ASSIGN exp:e
                  {: RESULT = new AssignExp(vleft, vright, v, e); :}
              |   simple_exp:s
                  {: RESULT = s; :}
              ;

simple_exp   ::=  com_exp:a1 relop:r com_exp:a2
                  {: RESULT = new OpExp(a1left, a1right, a1, r, a2); :}
              |   com_exp:a
                  {: RESULT = a; :}
              ;

/* 22. Combine Expression */
com_exp       ::= com_exp:e op:a factor:t
                  {: RESULT = new OpExp(eleft, eright, e, a, t); :}
              |   factor:t
                  {: RESULT = t; :}
              ;

/* 26. Factor  */
/* 27. Call  */
/* Combined Rule 27 into rule 26*/
factor      ::= LPAREN:l exp:e RPAREN:r
                {: RESULT = e; :}
            |   var:v
                {: RESULT = new VarExp(vleft, vright, v); :}
            |   ID:i LPAREN args:a RPAREN
                {: RESULT = new CallExp(ileft, iright, i, a); :}
            |   NUM:n
                {: RESULT = new IntExp(nleft, nright, n); :}
            ;

/* 19. Variable */
var           ::= ID:i
                  {: RESULT = new SimpleVar(ileft, iright, i); :}
              |   ID:i LSQBRK exp:e RSQBRK
                  {: RESULT = new IndexVar(ileft, iright, i, e); :}
              ;

/* 21. Relative Operation */
relop       ::= LTE 
                {: RESULT = OpExp.LE; :}
            |   LT
                {: RESULT = OpExp.LT; :}
            |   GT
                {: RESULT = OpExp.GT; :}
            |   GTE
                {: RESULT = OpExp.GE; :}
            |   EQ
                {: RESULT = OpExp.EQ; :}
            |   NE
                {: RESULT = OpExp.NE; :}
            ;

op          ::= PLUS
                {: RESULT = OpExp.PLUS; :}
            |   MINUS
                {: RESULT = OpExp.MINUS; :}
            |   TIMES
                {: RESULT = OpExp.MUL; :}
            |   OVER
                {: RESULT = OpExp.DIV; :}
            ;

/* 28. Arguments  */
/* 29. Argument List  */
/* Combined Rules 28 and 29 */
args        ::= args:a COMMA exp:e
                {: 
                    ExpList l = a;
                    if (l.head == null) {
                        l.head = e;
                    } else {
                        while( l.tail != null )
                            l = l.tail;
                        l.tail = new ExpList(e, null);
                    }
                    RESULT = a;
                :}
            |   exp:e
                {: RESULT = new ExpList(e, null); :}
            |   
                {: RESULT = new ExpList(null, null); :}
            ;

